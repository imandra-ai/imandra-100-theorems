(* An Imandra proof of the Divisibity by 3 Rule. 
   Theorem 85 in the 100 Theorems list. *)
(* G.Passmore, Imandra. *)

[@@@import "mod.iml"]

(* Sum of base-10 digits (non-negative n). Least significant digit first. *)
let rec sum_digits (n:int) : int =
  if n < 10 then n else (n mod 10) + sum_digits (n / 10)
[@@measure (Ordinal.of_int n)]
[@@by auto]

theorem ten_mod3_one =
  (10 mod 3) = 1
[@@by auto]

(* Main congruence: sum of digits preserves residue mod 3 *)
theorem sum_digits_preserves_mod3 n =
  (n >= 0) ==> (n mod 3) = (sum_digits n mod 3)
[@@by
  induction ()
  @>| [
    (* base: n < 10, sum_digits n = n *)
    auto;

    (* step: n >= 10 *)
    intros
    (* n = 10*(n/10) + (n mod 10) *)
    @> [%use Mod.Theorems.div_mod n 10]
    (* Take both sides mod 3 and distribute *)
    @> [%use Mod.Theorems.mod_add (10 * (n / 10)) (n mod 10) 3]
    @> [%use Mod.Theorems.mod_mul 10 (n / 10) 3]
    
    (* finish with the IH for n/10 and the definition of sum_digits *)
    @> [%use Mod.Theorems.mod_add (n mod 10) (sum_digits (n / 10)) 3]
    @> auto
  ]
]

(* Divisible by 3 iff digit-sum divisible by 3 *)
theorem divisibility_by_3_rule n =
  (n >= 0) ==> ((n mod 3 = 0) = (sum_digits n mod 3 = 0))
[@@by [%use sum_digits_preserves_mod3 n]
   @> auto]